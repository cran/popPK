$PROBLEM ORAL ADMINISTRATION STUDY
$DATA run1.csv IGNORE=@
$INPUT ID TIME AMT DV EVID MDV BW AGE ISM RACE DOSE
$SUBROUTINE ADVAN2 TRANS2
$PK
  CL = THETA(1)*(BW/70)**THETA(4)*EXP(ETA(1))
  V = THETA(2)*(BW/70)**THETA(5)*EXP(ETA(2))
  KA = THETA(3)*EXP(ETA(3))
  S2 = V/1000

$ERROR
  IPRED = F
  W=SQRT(THETA(6)**2+THETA(7)**2*IPRED**2) 
  IRES=DV-IPRED
  IWRES=IRES/W
  Y=IPRED+W*EPS(1)

$THETA  (0,2) ;CL
$THETA  (0,20) ;V
$THETA  (0,0.7) ;KA
$THETA  (0,5) ; CL-BW
$THETA (0,0.2) ;V-BW
$THETA  (0,5) ; Add 
$THETA (0,0.2) ;Prop

$OMEGA  0.1 ;CL 
$OMEGA  0.1 ;V 
$OMEGA  0.1 ;KA 

$SIGMA  1 FIX 

$ESTIMATION METH=1 INTER MAXEVAL=9999 PRINT=5
$COVARIANCE

$TABLE ID TIME AMT IPRED IWRES NOPRINT ONEHEADER FILE=sdtab1.csv
$TABLE ID CL V KA ETA1 ETA2 NOPRINT NOAPPEND ONEHEADER FILE=patab1.csv
$TABLE ID BW AGE NOPRINT NOAPPEND ONEHEADER FILE=cotab1.csv
$TABLE ID ISM RACE NOPRINT NOAPPEND ONEHEADER FILE=catab1.csv
